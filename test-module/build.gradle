ext {
    releaseVersion = '1.0.0'
}

apply plugin: 'idea'
apply plugin: 'java'
apply from: "${rootDir}/libraries.gradle"

allprojects {
    apply plugin: 'jacoco'

    version = '1.0.0-SNAPSHOT'
    group = 'io.test.gear4test'
    description = 'Gear4jTest'

    repositories {
        jcenter()
        mavenCentral()
    }
}

ext {
    coreProjects = subprojects.findAll {
        p -> !p.name.contains("documentation") && !p.name.endsWith("-bom")
    }
}

configure(project.coreProjects) {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    
    dependencies {
        compile(libraries.slf4j)

        testCompile(libraries.junit)
        testCompile(libraries.assertj)
        testCompile(libraries.logback)
        testCompile(libraries.mockito)
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.deprecation = true
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-Xlint:unchecked", "-parameters"]
    }
}

def allTestCoverageFile = "$buildDir/jacoco/allTestCoverage.exec"

task jacocoMergeTest(type: JacocoMerge) {
    destinationFile = file(allTestCoverageFile)
    executionData = project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
}

task jacocoMerge(dependsOn: ['jacocoMergeTest']) {
    // used to run the other merge tasks
}

test {
    dependsOn(subprojects.test) // required by cobertura to aggregate report
}
