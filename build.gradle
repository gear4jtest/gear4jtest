import java.util.stream.Collectors

plugins {
    id "org.sonarqube" version "3.0"
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version "4.18.3"
    id "me.champeau.gradle.jmh" version "0.5.3"
    id 'org.asciidoctor.convert' version '1.6.0'
    id 'idea'
    id 'io.freefair.aspectj' version '8.0.1'
    id 'jacoco-report-aggregation'
}

ext {
    releaseVersion = '1.0.0'
}

apply from: "${rootDir}/libraries.gradle"

allprojects {
    apply plugin: 'jacoco'

    version = '1.0.0-SNAPSHOT'
    group = 'io.test.gear4test'
    description = 'Gear4jTest'

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = "0.8.8"
    }
}

//artifactoryPublish.skip = true // apply to all projects except the root

ext {
    coreProjects = subprojects
}

dependencies {
    coreProjects.each { jacocoAggregation project(":${it.name}") }
}

configure(project.coreProjects) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'

    dependencies {
        testImplementation(libraries.junit)
        testImplementation(libraries.assertj)
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}

configure(project.coreProjects.stream().filter { !it.name.contains("gradle") }.collect(Collectors.toList())) {
    apply plugin: 'java-library'
    apply plugin: 'me.champeau.gradle.jmh'
    apply plugin: 'io.freefair.aspectj'
    apply from: "${rootDir}/publishing.gradle"

    dependencies {
        implementation(libraries.slf4j)
        implementation(libraries.log4jApi)
        implementation(libraries.log4jCore)
        implementation(libraries.cglib)
        implementation(libraries.aspectjrt)
        implementation(libraries.google)
//        implementation(libraries.aspectjweaver)

        //compileOnly(libraries.mojo)

        testImplementation(libraries.junit)
        testImplementation(libraries.assertj)
        testImplementation(libraries.logback)
        testImplementation(libraries.mockito)
        testImplementation(libraries.mockitoJunit)
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.deprecation = true
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-Xlint:unchecked", "-parameters"]
    }

    afterEvaluate {
        jar {
            inputs.property('moduleName', moduleName)
            manifest.attributes(
                    'Automatic-Module-Name': moduleName
            )
        }
    }

    test {
        systemProperty "jdk.internal.lambda.dumpProxyClasses", "/tmp/proxies/"
    }
}

configure(project.coreProjects.stream().filter { it.name.contains("gradle") }.collect(Collectors.toList())) {
    apply plugin: 'groovy-gradle-plugin'
}

def allTestCoverageFile = "${rootProject.projectDir}/build/reports/jacoco/report.xml"

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gear4jtest"
        property "sonar.projectName", "gear4jtest"
        property "sonar.projectKey", "gear4jtest_gear4jtest"
        property "sonar.links.homepage", "https://github.com/gear4jtest/gear4jtest"
        property "sonar.links.ci", "https://travis-ci.org/gear4jtest/gear4jtest"
        property "sonar.links.scm", "https://github.com/gear4jtest/gear4jtest"
        property "sonar.links.issue", "https://github.com/gear4jtest/gear4jtest/issues"
        property "sonar.language", "java"
        property "sonar.coverage.jacoco.xmlReportPaths", allTestCoverageFile
    }
}

task jacocoRootTestReport(type: JacocoReport) {

    coreProjects.each { dependsOn("${it.name}:test") }
    coreProjects.each { dependsOn("${it.name}:jacocoTestReport") }

    additionalSourceDirs.from = coreProjects.sourceSets.main.allSource.srcDirs
    sourceDirectories.from = coreProjects.sourceSets.main.allSource.srcDirs
    classDirectories.from = coreProjects.sourceSets.main.output
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')

    onlyIf {
        true
    }

    reports {
        xml.required = true
        xml.destination file(allTestCoverageFile)
        html.required = true
        csv.required = false
    }
}

tasks.check.dependsOn tasks.jacocoRootTestReport
tasks.jacocoRootTestReport.dependsOn tasks.test
